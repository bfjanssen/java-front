module metaborg_java_javac

imports

  libstratego-lib

  libstratego-gpp
  libspoofax/sdf/pp
  pp/metaborg-java-parenthesize
  pp/metaborg-java-pp
  
  injections/metaborg-java-injections

rules

  external javac(pp|)

  external javac(pp|expect)

  editor-compile:
    (_, _, ast, path, project-path) -> result
    with
      out := <implicate-injections-metaborg-java;javac(pp-java-string)> ast
    ; if Some(txt) := out then
        filename := <guarantee-extension(|"out")> path
      ; result := (filename, txt)
      else
        result := None()
      end

  pp-java-string =
    parenthesize-metaborg-java
    ; prettyprint-metaborg-java-start-symbols
    ; !V([], <id>)
    ; box2text-string(|120)

  /** Test expectations that invoke javac and test the compiler output. Use as follows:
   *    run javaSucceeds to ""
   *    run javaFails to ""
   *    run javaFails(|"(partial) expected message") to ""
   */
  javacSucceeds       = implicate-injections-metaborg-java;javac(pp-java-string|"")
  javacFails          = implicate-injections-metaborg-java;javac(pp-java-string|".+")
  javacFails(|expect) = implicate-injections-metaborg-java;javac(pp-java-string|$[.*[expect].*])

