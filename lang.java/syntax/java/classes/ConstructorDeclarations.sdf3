module java/classes/ConstructorDeclarations

// 8.8. Constructor Declarations

imports
  java/classes/ClassDeclarations
  java/lexical/Identifiers
  java/lexical/Modifiers
  java/classes/MethodDeclarations
  java/types/ParameterizedTypes
  java/names/Names
  java/interfaces/Annotations
  java/expressions/Main
  java/statements/Blocks

context-free sorts

  ConstructorDeclaration
  ConstructorModifier
  ConstructorInvocation

context-free syntax
  
  ConstructorDeclaration.ConstrDecl = <
  <{ConstructorModifier " "}*> <TypeParameters?> <Id>(<FormalParams>) <Throws?>{
    <ConstructorInvocation?>
    <{BlockStatement "\n"}*>
  }>
  
  ConstructorModifier = Annotation
  ConstructorModifier = Public
  ConstructorModifier = Private
  ConstructorModifier = Protected
  
  ConstructorInvocation.AltConstrInv      = <<TypeArguments?>this(<{Expression ", "}*>);>
  ConstructorInvocation.SuperConstrInv    = <<TypeArguments?>super(<{Expression ", "}*>);>
  ConstructorInvocation.ExprNameConstrInv = <<Expression>.<TypeArguments?>super(<{Expression ", "}*>);>