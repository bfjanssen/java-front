module java/expressions/ClassInstanceCreation

imports

  signatures/java/expressions/ClassInstanceCreation-sig

  java/classes/ClassDeclarations
  java/expressions/Main
  java/interfaces/Annotations
  java/names/Main
  java/names/MethodNames
  java/names/TypeNames
  java/names/PackageOrTypeNames
  java/types/ParameterizedTypes
  java/types/ReferenceTypes


rules // 15.9. Class Instance Creation Expressions

  exprOk(s, UnqualifiedInstance2Expression(unqualInst)) = T :-
    T == unqualInstOk(s, unqualInst).

  exprOk(s, QualifiedInstance(expr, unqualInst)) = T' :-
  {T}
    T == exprOk(s, expr),
    T' == qualInstOk(s, T, unqualInst),
    true.


  unqualInstOk : scope * UnqualifiedInstance -> TYPE

  unqualInstOk(s, NewInstance(typeArgsOpt, annoList, id, qualIdList, typeArgsOrDiamondOpt, exprList)) = typeDeclType(typeDecl) :-
  {qid Ts}
    _ == typeArgsOptOk(s, typeArgsOpt),
    annoListOk(s, annoList),
    qid == qualIdListOk(s, id, qualIdList),
    typeDecl == resolveTypeName(s, qid),
    typeArgsOrDiamondOptOk(s, typeArgsOrDiamondOpt),
    Ts == exprListOk(s, exprList),
    _ == resolveDirectMemberMthdName(typeDeclScope(typeDecl), Id("<init>"), Ts),
    true.

  unqualInstOk(s, NewInstance(typeArgsOpt, annoList, id, qualIdList, typeArgsOrDiamondOpt, exprList, clsBodyDeclList)) = REF(s_cls) :-
  {qid typeDecl Ts}
    _ == typeArgsOptOk(s, typeArgsOpt),
    annoListOk(s, annoList),
    qid == qualIdListOk(s, id, qualIdList),
    typeDecl == resolveTypeName(s, qid),
    typeArgsOrDiamondOptOk(s, typeArgsOrDiamondOpt),
    Ts == exprListOk(s, exprList),
    _ == resolveDirectMemberMthdName(typeDeclScope(typeDecl), Id("<init>"), Ts),

    new s_cls, s_cls -LEX-> s,

    setSuperClsOrImplementIntf(s_cls, typeDeclType(typeDecl)),

    clsBodyDeclListOk(s_cls, clsBodyDeclList).


  qualInstOk : scope * TYPE * UnqualifiedInstance -> TYPE

  qualInstOk(s, T, NewInstance(typeArgsOpt, annoList, id, qualIdList, typeArgsOrDiamondOpt, exprList)) = typeDeclType(typeDecl) :-
  {s_ty Ts}
    _ == typeArgsOptOk(s, typeArgsOpt),
    annoListOk(s, annoList),
    s_ty == typeScope(T),
    typeDecl == singleTypeDecl(id, resolveMemberTypeNames(s_ty, id)),
    typeArgsOrDiamondOptOk(s, typeArgsOrDiamondOpt),
    Ts == exprListOk(s, exprList),
    _ == resolveDirectMemberMthdName(typeDeclScope(typeDecl), Id("<init>"), Ts),
    true.

  qualInstOk(s, T, NewInstance(typeArgsOpt, annoList, id, qualIdList, typeArgsOrDiamondOpt, exprList, clsBodyDeclList)) = REF(s_cls) :-
  {s_ty typeDecl Ts}
    _ == typeArgsOptOk(s, typeArgsOpt),
    annoListOk(s, annoList),
    s_ty == typeScope(T),
    typeDecl == singleTypeDecl(id, resolveMemberTypeNames(s_ty, id)),
    typeArgsOrDiamondOptOk(s, typeArgsOrDiamondOpt),
    Ts == exprListOk(s, exprList),
    _ == resolveDirectMemberMthdName(typeDeclScope(typeDecl), Id("<init>"), Ts),

    new s_cls, s_cls -LEX-> s,

    setSuperClsOrImplementIntf(s_cls, typeDeclType(typeDecl)),

    clsBodyDeclListOk(s_cls, clsBodyDeclList).


  qualIdListOk : scope * Id * list(QualifiedId) -> QID
    qualIdListOk1 : scope * QID * list(QualifiedId) -> QID

  qualIdListOk(s, id, qualIdList) = qualIdListOk1(s, ID(id), qualIdList).

    qualIdListOk1(s, qid, []) = qid.

    qualIdListOk1(s, qid, [QualifiedId(annoList, id)|qualIdList]) = qualIdListOk1(s, QID(qid, id), qualIdList) :-
      annoListOk(s, annoList).


  typeArgsOrDiamondOk : scope * TypeArgumentsOrDiamond
  typeArgsOrDiamondOptOk maps typeArgsOrDiamondOk(*, list(*))

  typeArgsOrDiamondOk(s, TypeArguments2TypeArgumentsOrDiamond(typeArgs)) :-
    _ == typeArgsOk(s, typeArgs).

  typeArgsOrDiamondOk(s, Diamond()) :-
    true.


